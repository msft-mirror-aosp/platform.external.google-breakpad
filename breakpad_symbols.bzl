"""Bazel rule for extracting breakpad symbols.

The rule extracts symbols from binaries using the `dump_syms` tool.
For each input binary, it generates a `.sym` file.

### Output Location
The generated `.sym` files are located in the Bazel output directory under the
package where the rule is instantiated. To avoid filename collisions when
processing multiple binaries with the same name from different directories,
the output files are organized in subdirectories named after the hash of the
input binary's directory path:

  `<bazel-bin>/<package_path>/<target_name>/<hash(binary_dir)>/<binary_name>.sym`

For example, if the rule is instantiated as `extract_symbols` in `emulator/`
and processes `bin/launcher`, the output will be at:
  `bazel-bin/emulator/extract_symbols/<hash(bin)>/launcher.sym`

These files are returned in the `DefaultInfo` provider of the rule.
"""

load("@bazel_skylib//lib:paths.bzl", "paths")
load(
    "@goldfish_build//rules/native:debug.bzl",
    "DebugSymbolsSetInfo",
    "FissionPackageSetInfo",
    "collect_fission_package_aspect",
    "collect_pdb_aspect",
    "gen_dsym_aspect",
)

visibility(["public"])

def windows_path(p):
    # type: (string) -> string
    return p.replace("/", "\\")

def _maybe_executable(file):
    # type: (File) -> bool
    if not file.extension:  # Assume files with no extension are executables.
        return True
    if file.extension in ["so", "dylib", "exe", "dll"]:
        return True
    stem, extension = paths.split_extension(file.basename)
    if extension[1:].isdigit() and stem.endswith(".so"):
        # liba.so.1
        return True
    return False

def _breakpad_symbols_impl(ctx):
    """Extracts symbols from binaries using dump_syms.

    This function iterates over a list of binaries, uses `dump_syms` to
    generate a `.sym` file for each binary, and returns a `DefaultInfo` provider
    containing the generated symbol files.

    Args:
        ctx: The rule context.

    Returns:
        A `DefaultInfo` provider containing the generated symbol files.
    """
    is_windows = ctx.target_platform_has_constraint(ctx.attr._target_windows[platform_common.ConstraintValueInfo])

    # Windows symbol extraction (PE/PDB) requires the Microsoft DIA (Debug Interface Access) SDK,
    # which is a COM-based API only available on Windows. We disable symbol extraction
    # when targeting Windows if we are not running on a Windows execution environment
    # (detected by the absence of the .exe extension on the dump_syms tool).
    if is_windows and not ctx.executable._dump_syms.path.endswith(".exe"):
        return DefaultInfo(files = depset())

    split_symbol_lookup = {}
    for binary in ctx.attr.binaries:
        if DebugSymbolsSetInfo in binary:
            symbol_set = binary[DebugSymbolsSetInfo]
            if symbol_set.dsym:
                for symbol in symbol_set.dsym.to_list():
                    split_symbol_lookup[symbol.executable_file] = ("dsym", symbol.original_executable_file, symbol.dsym_bundle)
            if symbol_set.pdb:
                for symbol in symbol_set.pdb.to_list():
                    split_symbol_lookup[symbol.executable_file] = ("pdb", symbol.original_executable_file, symbol.pdb_file)
        if FissionPackageSetInfo in binary:
            symbol_set = binary[FissionPackageSetInfo]
            if symbol_set.fission_package:
                for symbol in symbol_set.fission_package.to_list():
                    split_symbol_lookup[symbol.executable_file] = ("dwp", symbol.original_executable_file, symbol.dwp_file)

    output_files = []
    ignored_suffixes = tuple(ctx.attr.ignore_paths_with_suffix)

    for candidate in ctx.files.binaries:
        if ignored_suffixes and candidate.path.endswith(ignored_suffixes):
            continue

        if candidate not in split_symbol_lookup and not _maybe_executable(candidate):
            continue

        input_file = candidate
        input_files = [input_file]
        prepend_args = []
        append_args = []
        if input_file in split_symbol_lookup:
            symbol_type, original_executable, split_symbol = split_symbol_lookup[input_file]
            input_file = original_executable or input_file
            input_files = [input_file, split_symbol]
            if symbol_type == "dsym":
                prepend_args.extend(["-g", split_symbol.path])
            elif symbol_type == "dwp":
                append_args.append(split_symbol.dirname)
            elif symbol_type == "pdb":
                input_file = split_symbol
                prepend_args.append("--i")  # Generate INLINE/INLINE_ORIGIN records, only valid when reading PDBs
        elif input_file.extension in ["exe", "dll"]:
            prepend_args.append("--pe")  # No PDB available - read from PE instead.

        output_name = str(
            hash(candidate.dirname),
        ) + "/" + paths.replace_extension(candidate.basename, ".sym")
        output_file = ctx.actions.declare_file(output_name)
        output_files.append(output_file)

        if ctx.target_platform_has_constraint(
            ctx.attr._target_windows[platform_common.ConstraintValueInfo],
        ):
            ctx.actions.run(
                mnemonic = "ExtractBreakpadSymbols",
                outputs = [output_file],
                inputs = input_files,
                executable = ctx.executable._dump_syms,
                arguments = prepend_args + [
                    "--f",  # Output to:
                    windows_path(output_file.path),
                    windows_path(input_file.path),
                ] + append_args,
            )
        else:
            ctx.actions.run(
                mnemonic = "ExtractBreakpadSymbols",
                outputs = [output_file],
                inputs = input_files,
                executable = ctx.executable._dump_syms,
                arguments = prepend_args + [
                    "-d",  # Generate INLINE/INLINE_ORIGIN records
                    "-m",  # Handle multiple symbols at same address, if any.
                    "-f",  # Output to:
                    output_file.path,
                    input_file.path,
                ] + append_args,
            )

    return DefaultInfo(files = depset(output_files))

# Define the rule
breakpad_symbols = rule(
    implementation = _breakpad_symbols_impl,
    fragments = ["platform"],
    attrs = {
        "binaries": attr.label_list(
            allow_files = True,
            mandatory = True,
            doc = "The list of binaries to extract symbols from.",
            aspects = [
                gen_dsym_aspect,
                collect_fission_package_aspect,
                collect_pdb_aspect,
            ],
        ),
        "ignore_paths_with_suffix": attr.string_list(
            doc = "Suffixes of binary paths that should cause the binary to be ignored.",
        ),
        "_dump_syms": attr.label(
            default = Label("//:dump_syms"),
            allow_single_file = True,
            executable = True,
            cfg = "exec",
            doc = "The dump_syms executable. Defaults to //:dump_syms.",
        ),
        "_target_windows": attr.label(default = "@platforms//os:windows"),
    },
)
